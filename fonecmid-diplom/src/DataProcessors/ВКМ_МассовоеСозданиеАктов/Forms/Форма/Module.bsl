#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Объект.Период = ТекущаяДатаСеанса();
	
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура СоздатьРеализации(Команда)
	
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ДлительнаяОперация = СоздатьДокументыНаСервере();
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.Интервал = 3;
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	ПараметрыОжидания.ОповещениеОПрогрессеВыполнения = Новый ОписаниеОповещения("ОповещениеОПрогрессеВыполнения", ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОповещениеОЗавершении", ЭтотОбъект);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
		

КонецПроцедуры
#КонецОбласти


#Область СлужебныеПроцедурыИФункции
&НаСервере
Функция СоздатьДокументыНаСервере()

	СоздатьДокументы = ДлительныеОперации.ВыполнитьФункцию(УникальныйИдентификатор, 
	
		"Обработки.ВКМ_МассовоеСозданиеАктов.СозданиеАктов", Объект.Период);
		
	Возврат СоздатьДокументы;

КонецФункции


&НаКлиенте
Процедура ОповещениеОЗавершении(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Статус = "Выполнено" Тогда
		
		РезультатВыполненияОперации = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		
		Объект.ДоговорыИРеализации.Очистить();

		Для Каждого СрокаДанных Из РезультатВыполненияОперации.МассивСтрок Цикл
			НоваяСтрока = Объект.ДоговорыИРеализации.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СрокаДанных);
		КонецЦикла;
		
		
		ТекстСообщения = СтрШаблон("Количество созданных реализаций = %1", РезультатВыполненияОперации.СозданоДокументов);
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = ТекстСообщения;
		Сообщение.Сообщить();

		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеОПрогрессеВыполнения(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Прогресс <> Неопределено Тогда
		Состояние("Прогресс операции", Результат.Прогресс.Процент);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти